# ArchW nemo-share tmp patched version

_pkgbase=nemo-extensions
pkgbase=${_pkgbase}-git
_pkgname=('nemo-share')
pkgname=()
for _p in "${_pkgname[@]}" ; do 
	pkgname+=(${_p}-git)
done
_pkgver=4.0.0
pkgver=4.0.0+120+gebe9cf2
pkgrel=1
pkgdesc="Nemo share extention"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
depends=('nemo')
makedepends=(intltool gobject-introspection meson samurai)
options=('!emptydirs')
source=("${pkgbase}::git+https://github.com/linuxmint/nemo-extensions")
sha256sums=('SKIP')
_release_commit='cdb9295e31d70924e5f7de2367b596f8e191a7cd'

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s+%s+%s" \
               "${_pkgver}" \
               "$( bc <<< "`git rev-list HEAD --count` - `git rev-list ${_release_commit} --count`" )" \
               "$( git describe --long | sed 's:.*-::' )"
}

prepare() {
    cd "${srcdir}/${pkgbase}"

    # Patch nemo-share
    sed -i -E "s:(argv\[1\] =) \"-l\":\1 \"--long\":g" nemo-share/src/shares.c
}

build() {
    cd "${srcdir}/${pkgbase}"

    for _dir in "${_pkgname[@]}"; do
      if [[ -f ${_dir}/meson.build ]]; then
        mkdir -p ${_dir}/build
        pushd ${_dir}/build
        arch-meson --libexecdir=lib/${_dir} ..
        samu
        popd
      fi
    done
}

package_nemo-share-git() {
    pkgdesc="Samba extension for Nemo"
    depends+=(samba)
    optdepends=('cinnamon-translations: i18n')

    cd ${srcdir}/${pkgbase}/${pkgname/-git/}/build
    DESTDIR="${pkgdir}" samu install
}
